;
; Copyright (c) Microsoft Corporation.  All rights reserved.
;
;
; Use of this sample source code is subject to the terms of the Microsoft
; license agreement under which you licensed this sample source code. If
; you did not accept the terms of the license agreement, you are not
; authorized to use this sample source code. For the terms of the license,
; please see the license agreement between you and Microsoft or, if applicable,
; see the LICENSE.RTF on your install media or the root of your tools installation.
; THE SAMPLE SOURCE CODE IS PROVIDED "AS IS", WITH NO WARRANTIES.
;
;;-----------------------------------------------------------------------------
;;
;;  File:  flushic.src
;;
;;-----------------------------------------------------------------------------

    .include "kxshx.h"
    .include "shx.inc"

    .align 4

;;-----------------------------------------------------------------------------
;;
;;  Function :  SH4FlushICache
;;
;;  Flush and invalidate the Instruction Cache
;;-----------------------------------------------------------------------------
    
    LEAF_ENTRY _SH4FlushICache
    mov     #h'A0000000, r2     ; (r2) = un-cached, un-mapped region base
    mov.l   IC1, r1
    or      r2, r1              ; (r1) = un-cached destination
    jmp     @r1                 ; jump to un-cached region
    nop

    ; SH4 cpu architecture requires when modifying CCR from P2 area (un-cached),
    ; at least 8 instructions must be in between before issuing a branch
    ; to U0/P0/P1/P3 area.
    
    .align 4
IC1:    .data.l IC2
IC2:
    mov     #CCN_CCR, r1
    mov.l   @r1, r0
    mov     #h'00000800, r2
    or      r2, r0
    mov.l   r0, @r1             ; invalidate I-cache            

    ; Wait for 7 NOPs after changing CCN_CCR
    nop
    nop
    nop
    nop
    nop
    nop
    nop

    mov.l   IC3, r2             ; the 8th instructon before branch 
    
    jmp     @r2                 ; jump to cached region
    nop
    
    .align 4
IC3:    .data.l IC4
IC4: 
    rts
    nop                         ; delay slot

    .endf

;;-----------------------------------------------------------------------------

    .end
